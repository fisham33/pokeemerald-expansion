mapscripts Campsite1_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_0, 0: Campsite1_EventScript_SpawnParty
    ]
}

script Campsite1_EventScript_SpawnParty {
    special(Camping_SpawnParty)
    setvar(VAR_TEMP_0, 1)
    end
}

script Campsite_EventScript_Table {
    lockall
    msgbox("Would you like to have a meal?", MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES) {
        special(Camping_SelectPokeblock)
        if (var(VAR_RESULT) != 0) {
            call(Campsite_EventScript_MealCutscene)
        }
    }
    releaseall
    end
}

script Campsite_EventScript_Tent {
    lockall
    msgbox("Rest and pack up camp?", MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES) {
        special(Camping_ExitCamping)
    }
    releaseall
    end
}

script Campsite_EventScript_MealCutscene {
    msgbox("Everyone gathered around for a meal!")
    playfanfare(MUS_HEAL)
    waitfanfare
    msgbox("Everyone's HP was restored!")
    return
}

script Campsite_EventScript_CampingPokemon {
    lock
    faceplayer
    special(Camping_InteractWithPokemon)
    playmoncry(VAR_0x8004, CRY_MODE_NORMAL)
    msgbox(format("{STR_VAR_1} seems happy!"))
    waitmoncry
    release
    end
}
